!************************************************************!
!** SCRIPT TUNEL AXISSIMÉTRICO                             **!
!** Versão: 2023.8                                         **!
!**                                                        **!
!** Objetivo: faz análise da convergência de tuneis        **!
!**           profundos (PROFUNDIDADE>10R) com seção       **!
!**           circular considerando modelo axissimétrico.  **!
!**                                                        **!
!** Programador: Felipe Quevedo e Carlos Alberto           **!
!** Inicio     : (01/04/2016)                              **!
!** Situação   : Em teste                           	   **!
!** Unidades   :[F/A, L, t] = MPa,m,dia			   **!
!**                                                        **!
!************************************************************!
!
!************************************************************!
!****_ 1. INICIALIZANDO O ANSYS 		        _****!
!************************************************************!
FINISH
/CLEAR,NOSTART
!
!************************************************************!
!****_ 2. DADOS DE ENTRADA DO PROBLEMA 		        _****!
!************************************************************!
!
! Nessa seção define-se todos os dados de entrada necessarios
!
!************************************************************!
!****_ 		2.1 Parâmetros geométricos		_****!
!************************************************************!
Re 		= 1 		! [m] raio da interface entre o túnel e o maciço
esp		= 0.03		! [m] espessura do revestimento
Lp		= 1/3*Re	! [m] comprimento do passo de escavação
nd0		= 2		! [Lp] número de passos que representam a dimensão não suportada
npi		= 3		! [Lp] número de passos escavados na primeira escavação
Vp 		= 12.5		! [m/dia] velocidade do passo de escavação
revested0 	= 1		! 0 - não reveste ultimo d0+Lp, 1 - reveste ultimo d0+Lp
revesteface 	= 1		! 0 - não reveste a face, 1 - reveste a face
!
! referente ao restante do domínio
l1		= 10 		! [Re] comprimento não escavado
l2		= 100		! [Lp] comprimento do trecho escavado
l3		= 20		! [Re] dimensão vertical do maciço
!
!
!************************************************************!
!****_  	2.2 Parâmetros constitutivos do maciço 	_****!
!************************************************************! 		   
! Tipo de material do maciço (matmacico)
!
! 	1  - elastico do ANSYS
! 	2  - elastoplastico VM do ANSYS (BISO)
! 	22 - elastoplastico VM do ANSYS (MISO)
!	23 - viscoplastico Perzyna VM do ANSYS
!	4  - usermat3D_VM do ANSYS (BISO)
!	5  - usermat3D_elastico
!	6  - usermat3D_EP
!	7  - usermat3D_VP
!	8  - usermat3D_EPVP	                 
!
matmacico	= 8
!                                
! Parâmetros de todos os modelos 
E1 		= 1500		 ! [MPa] modulo elasticidade (1,2,22,23,4,5,6,7,8)
nu1 		= 0.498		 ! [adm] coeficiente de poisson (1,2,22,23,4,5,6,7,8)
!                                
! Parametros para o modelo 2, 22, 6 e 8
superficief	= 2		 ! função de escoamento: 1-DPI, 2-DPII, 3-DPIII (6,8)
superficieg 	= 2		 ! função potencial: 1-DPI, 2-DPII, 3-DPIII (6,8)
fi	    	= 0		 ! [graus] angulo de atrito (0 - VM ou TR) (6,8)
psi	    	= fi		 ! [graus] angulo de dilatância (6,8)
c1	    	= 4*SQRT(3)/2 ! [MPa] coesão inicial (2,22,23,4,6,8)
c2	    	= 4*SQRT(3)/2 ! [MPa] coesão de pico (2,22,23,4,6,8)
c3	    	= 4*SQRT(3)/2 ! [MPa] coesão residual (22,6,8)
eps1	    	= 0.010		 ! [adm] deformação equiv. limite da zona 1 (22,6,8)
eps2	    	= 0.010		 ! [adm] deformação equiv. limite da zona 2 (22,6,8)
eps3	    	= 0.010		 ! [adm] deformação equiv. limite da zona 3 (22,6,8)
Dalg		= 0		 ! 0 - módulo elastico, 1 - módulo algoritmíco (6,8)
!                                
! Parametros para o modelo 23, 7 e 8
superficiefvp 	= 2		 ! função de escoamento: 1-DPI, 2-DPII, 3-DPIII (7,8)
superficiegvp 	= 2		 ! função potencial: 1-DPI, 2-DPII, 3-DPIII (7,8)
fivp          	= 0		 ! [graus] angulo de atrito (0 - VM ou TR)(7,8)
psivp         	= fivp		 ! [graus] angulo de dilatância (7,8)
cvp 	      	= 2*SQRT(3)/2	 ! [MPa] coesão viscoplastica (23,7,8)
n1	      	= 1		 ! [adm] expoente do modelo de Perzyna (23,7,8)
eta           	= 4E4 		! [dia] constante de viscosidade dinâmica (23,7,8)
f0     	      	= 1		 ! [MPa] valor de referência (23,7,8)
thetavp       	= 1		 ! 0 - totalmente explícito, 1 - semi-implicito (7,8)
!
!********************************************************************!
!****_ 		2.3 Parâmetros constitutivos do revest	_****!
!********************************************************************!
! Tipo de material
!	0 - Sem revestimento
!	1 - Ansys_Elástico
!	2 - Usermat_Viscoelastico_CEB-MC90
!
matrev 		= 2		! Tipo de material (0-SR,1-E,2-VE)
!
! Parâmetros para os modelos 1 e 2
E2 		= 30000		! [MPa] módulo de elasticidade do revestimento (matrev=1)
nu2 		= 0.3    	! [adm] coeficiente de Poisson do revestimento (matrev=1,2)		
!
! Parâmetros para o modelo 2
fck 		= 20		! [MPa] resistência característica do concreto (2)
s 		= 0.2		! [adm] coef. que depende do tipo de concreto (2)
ti 		= 0		! [dias] início do concreto (2)
rh 		= 70		! [%] umidade relativa do ambiente (2)
hf 		= (Re**2-(Re-esp)**2)/(Re-esp)	! [m] espessura fictítica (QUEVEDO, 2017, p. 84) (2)
ts 		= 7		! [dias] idade do concreto no início da secagem (2)
t0		= 1		! [dias] idade do concreto no início do carregamento (2)
tinf		= 3000		! [dias] tempo final da análise (2)
betasc 		= 8d0		! [adm] coeficiente que depende do tipo de cimento (2)
temperatura 	= 20		! [oC] temperatura (2)
alpha 		= 1		! [adm] efeito do tipo de cimento durante a cura (2)
ndec  		= 10		! [década] No. de pontos por decada na discretização (2)
ligafluencia 	= 1		! [un] 1 - liga fluência, 0 - desliga fluência (2)
ligaretracao 	= 1		! [un] 1 - liga retração, 0 - desliga retração (2)
ktime		= 10		! [dias] tempo da análise para saída de dados
kelemid		= 2060		! [un] identificação do elemento para saída
kkDomIntPt	= 1		! [un] identificação do ponto de integração para saída
!
! Calculo do módulo
fcm 		= fck + 8
E2 		= 21500*(fcm/10)**(1/3)		    	       
!************************************************************!
!****_ 		2.4 Condições de Contorno	  	_****!
!************************************************************!
pr		= 9		! [MPa] tensões iniciais e pressão hidrostáticas
!
!************************************************************!
!****_ 		2.5 Discretização do tempo nos passos	_****!
!************************************************************!
!
! Discretização do tempo durante os passos de escavação
tp		= Lp/(Vp)	! [dias] tempo de cada passo
dtp 		= 0.5*tp	! [dias] incremento de tempo durante o cálculo
dtpmax 		= dtp		! [dias] incremento de tempo máximo
!
! Discretização do tempo nos passos após a escavação
tp2		= 100		! [dias] tempo de cada passo de solução após a escavação do túnel
np2max		= 30		! [un] número máximo de passos de solução após a escavação do túnel
dtp2		= 0.5*tp2	! [dias] incremento de tempo de cada passo
!
tp2autots	= 1		! Especifica quando usar incremento de tempo automático
!				!      -1 - O programa escolhe
!				! 	0 - não usa incremento de tempo automático (usa apenas dtp2)
!				!	1 - usa incremento de tempo automático (usa o dpt2min e dpt2max)
tp2carry	= 1		! Incremento de tempo entre passos
!				!	0 - usa o dtp2 como o incremento de tempo em cada passo
!				!	1 - usa o incremento de tempo do passo anterior em cada passo (se dtp2autots = 1)
dtp2min		= tp2/2		! [dias] incremento mínimo de tempo de cada passo 
dtp2max		= tp2		! [dias] incremento máximo de tempo de cada passo				! 
!
deltaumin	= 1E-8		! [adm] critério de convergência no tempo
!
!************************************************************!
!****_ 		2.6 Discretização da malha              _****!
!************************************************************!
tipoelem 	= 1		! 1 - PLANE182, 2 - PLANE183
formaelem 	= 0		! 0 - quadrilátero, 1 - triangular
formaint	= 0		! integração 0 - completa, 1 - reduzida
tipomalha	= 1		! 0 - malha livre, 1 - mapeada, 2 - mapeada se possível
nLp		= 1		! [un] numero de elementos no passo escavado
nL1 		= 15		! [un] numero de elementos trecho não escavado
mL1 		= 1/5		! [adm] taxa de crescimento
nesp		= 1 		! [un] divisões na espessura do revestimento
nRi 		= 6		! [un] numero de elementos na vertical do maciço escavado
nLx 		= 20		! [un] numero de elementos acima da seção do túnel
mLx 		= 40		! [adm] taxa de crescimento
!
!************************************************************!
!****_ 		2.7 Configuração da saida de dados    	_****!
!************************************************************!
iso 		= 0		! 0 - contorno padrão, 1 - isosuperficie
flag1 		= 1		! 1 - saida jpg do gráfico de convergência em arquivo
flag2 		= 1		! 1 - saida txt de resultados do gráfico de convergência
flag3 		= 1		! 1 - saida do gráfico de convergência na tela do Ansys
!
! Obs: os arquivos de imagem jpg saem com o nome jobname###.jpg
!  onde ### é um inteiro que vai incrementando a partir da última
!  numeração do último arquivo plotado.
!  Os campos serão referentes ao final da análise.
!
!************************************************************!
!****_ 		2.8 Configurações da solução	     	_****!
!************************************************************! 
!
nr	= 2	! Tipo de Newton-Raphson
!		! 1 - INIT rigidez não é atualizada
!		! 2 - FULL rigidez atualizada a cada iteração
!		! 3 - UNSYM assimétrica atualizada a cada iteração
!
! Tolerâncias durante os passos de escavação do túnel longitudinal
tolf	= 1e-6	! tolerância para o resíduo (padrão 0.005)
tolu	= 1e-4	! tolerância para o incremento de deslocamento (padrão 0.05)
!
! Tolerâncias durante os passos após a construção do túnel
tolf2	= 1e-6	! tolerância para o resíduo
tolu2	= 1e-4	! tolerância para o incremento de deslocamento
!
! Tolerância para o incremento de deformação plástica equivalente
plslimit = 0.15
! 
psc	= 1	! 1 - Ativa, 0 - desativa a opção de paralelização SMP
nlg	= 0	! 1 - Ativa, 0 - desativa a não lineariedade geométrica
soleq	= 0	! 1 - calcula o equilibrio do maciço antes de iniciar
!
!************************************************************!
!****_ 		2.9 Titulo e nome dos arquivos  	_****!
!************************************************************!
! defini titulo na tela do Ansys
/TITLE, Re=%Re% matmacico=%matmacico% E1=%E1% pr=%pr% matrev=%matrev% E2=%E2% Vp=%Vp%
!
! define nome file.* para os arquivos da analise
/FILNAME,file,0
!
!
!
!
!------------------FIM ENTRADA DE DADOS---------------------!
!
!
!
!
!************************************************************!
!****_ 3. PRÉ-PROCESSAMENTO 				_****!
!************************************************************!
/PREP7	! inicia módulo de pré-processamento
!
!************************************************************!
!****_ 		3.1 Calculos iniciais			_****!
!************************************************************!
ri 		= re-esp	! [m] raio da face interna do revestimento do túnel
l1		= l1*Re    	! [m] comprimento do trecho não escavado
l2		= l2*Lp		! [m] comprimento do trecho escavado
l3		= l3*Re	   	! [m] dimensão vertical do maciço
lt 		= l1+l2 	! [m] comprimento total do modelo ao longo do eixo longitudinal
d0		= nd0*lp	! [m] dimensão não suportada (multiplo do passo)
np 		= l2/Lp 	! [un] numero total passos escavados nos tuneis longitudinais
nesc		= np-npi+1	! [un] total de escavações
nl2 		= np*nlp   	! [un] numero de elementos no trecho escavado
!
! verificando se haverá análise após a construção do túnel
analise2 = 0
*IF,matmacico,GT,6,OR,matrev,GT,1,THEN
	analise2 = 1
*ENDIF
*IF,matmacico,EQ,23,THEN
	analise2 = 1
*ENDIF
!
!************************************************************!
!****_ 		3.2 Elementos finitos 			_****!
!************************************************************!
*IF,tipoelem,EQ,1,THEN
	ET,1,PLANE182
*ELSE
	ET,1,PLANE183
*ENDIF
KEYOPT,1,1,formaint
KEYOPT,1,3,1
KEYOPT,1,6,0
!
!************************************************************!
!****_ 		3.3 Material do maciço 			_****!
!************************************************************!
*IF,matmacico,eq,1,then
	! Definindo o material elastico do Ansys
	MPTEMP,1,0  		
	MPDATA,EX,1,,E1	
	MPDATA,PRXY,1,,nu1 	
*ELSEIF,matmacico,eq,2,then
	! Definindo o material elastoplástico VM do Ansys bilienar
	yield	    	= 2*c1			! [MPa]	tensão de escoamento
	Ep 		= 2*(c2-c1)/(eps1)	! [MPa] módulo plástico tangente
	youngt      	= Ep*E1/(E1+Ep)		! [MPa] módulo de elastoplástico tangente
	!
	MPTEMP,1,0  		
	MPDATA,EX,1,,E1	
	MPDATA,PRXY,1,,nu1 	
	TB,BISO,1,1,2,  
	TBTEMP,0
	TBDATA,,yield,youngt,,,,
*ELSEIF,matmacico,eq,22,then
	! Definindo o material elastoplástico VM do Ansys multilinear
	yield	    	= 2*c1			! [MPa]	tensão de escoamento
	Ep 		= 2*(c2-c1)/(eps1)	! [MPa] módulo plástico tangente
	youngt      	= Ep*E1/(E1+Ep)		! [MPa] módulo de elastoplástico tangente
	epst0		= 2*c1/E1		! [adm] deformação total no escoamento
	epst1		= 2*c2/E1 + eps1 	! [adm] deformação total na zona 1
	epst2		= 2*c2/E1 + eps2	! [adm] deformação total na zona 2	
	epst3		= 2*c3/E1 + eps3	! [adm] deformação total na zona 3
	!
	MPTEMP,1,0  		
	MPDATA,EX,1,,E1	
	MPDATA,PRXY,1,,nu1 	
	TB,MISO,1,1,5,0 
	TBTEMP,0
	TBPT,,epst0,2*c1
	TBPT,,epst1,2*c2
	TBPT,,epst2,2*c2 
	TBPT,,epst3,2*c3
	TBPT,,0.1,2*c3    
*ELSEIF,matmacico,eq,23,then
	! Definindo o material viscoplastico perzyna VM do Ansys
	yieldvp	    	= 2*cvp				! [MPa]	tensão de escoamento
	youngtvp      	= 0				! [MPa] módulo de elastoplástico tangente
	gamma	      	= ((yieldvp/f0)**n1)/eta 	! [dias^-1] coeficiente Gamma do modelo de Perzyna Ansys
	m  	      	= 1/n1				! [adm] coeficiente m do modelo de Perzyna do ANSYS
	!
	MPTEMP,1,0  		
	MPDATA,EX,1,,E1	
	MPDATA,PRXY,1,,nu1	
	TB,BISO,1,1,2,  
	TBTEMP,0
	TBDATA,,yieldvp,youngtvp,,,,
	TB,RATE,1,1,2,1
	TBTEMP,0
	TBDATA,,m,gamma,,,,	
*ELSEIF,matmacico,eq,4,then
	! Definindo material usermat3D VM do Ansys
	yield	    	= 2*c1			! [MPa]	tensão de escoamento
	Ep 		= 2*(c2-c1)/(eps1)	! [MPa] módulo plástico tangente
	youngt      	= Ep*E1/(E1+Ep)		! [MPa] módulo de elastoplástico tangente
	!
	TB,USER,1,2,5
	TBTEMP,1.0 		
	TBDATA,1,matmacico,E1,nu1,yield,youngt ! modelo, E, posn, sigy, H
	TB,STATE,1,,8
*ELSEIF,matmacico,eq,5,then
	! Definindo material usermat3D_elastico
	TB,USER,1,2,3
	TBTEMP,1.0 		
	TBDATA,1,matmacico,E1,nu1
	TB,STATE,1,,8	
*ELSEIF,matmacico,eq,6,then
	! Definindo material usermat3D_EP
	TB,USER,1,2,14
	TBTEMP,1.0 		
	TBDATA,1,matmacico,E1,nu1
	TBDATA,4,superficief,superficieg
	TBDATA,6,fi,psi,
	TBDATA,8,c1,c2,c3,eps1,eps2,eps3
	TBDATA,14,Dalg
	TB,STATE,1,,20	
*ELSEIF,matmacico,eq,7,then
	! Definindo material usermat3D_VP
	TB,USER,1,2,13
	TBTEMP,1.0 		
	TBDATA,1,matmacico,E1,nu1
	TBDATA,4,superficiefvp,superficiegvp
	TBDATA,6,fivp,psivp,
	TBDATA,8,cvp,n1,eta,f0,thetavp	
	TB,STATE,1,,20		
*ELSEIF,matmacico,eq,8,then
	! Definindo material usermat3D_EPVP
	TB,USER,1,2,23
	TBTEMP,1.0 		
	TBDATA,1,matmacico,E1,nu1
	TBDATA,4,superficief,superficieg
	TBDATA,6,fi,psi,
	TBDATA,8,c1,c2,c3,eps1,eps2,eps3
	TBDATA,14,Dalg
	TBDATA,15,superficiefvp,superficiegvp
	TBDATA,17,fivp,psivp
	TBDATA,19,cvp
	TBDATA,20,n1,eta,f0
	TBDATA,23,thetavp	
	TB,STATE,1,,20
*ENDIF
!
!************************************************************!
!****_ 		3.4 Material do revestimento		_****!
!************************************************************!
*IF,matrev,EQ,1,THEN
	*DO,i,2,nesc,1
		MPTEMP,1,0
		MPDATA,EX,i,,E2
		MPDATA,PRXY,i,,nu2
	*ENDDO
*ELSEIF,matrev,EQ,2,THEN
	*DO,i,2,nesc,1
		TB,USER,i,1,18
		TBTEMP,1.0
		TBDATA,1,9
		TBDATA,2,ligafluencia,ligaretracao
		TBDATA,4,npi*tp+tp*(i-2)
		TBDATA,5,ts,t0,tinf
		TBDATA,8,fck,nu2
		TBDATA,10,s,rh,hf,betasc,alpha
		TBDATA,15,ndec
		TBDATA,16,ktime,kelemid,kkDomIntPt
		TB,STATE,i,,500
	*ENDDO
*ENDIF
!
!************************************************************!
!****_ 		3.5 Modelando o sólido			_****!
!************************************************************!
! Definindo a vista
/VIEW,1,,,1
/ANG,1
!
! criando pontos notaveis
K,1,0,0
K,2,0,l1
K,3,0,lt
K,4,ri,0
K,5,ri,l1
K,6,ri,lt
K,7,re,0
K,8,re,l1
K,9,re,lt
K,10,l3,0
K,11,l3,l1
K,12,l3,lt
!
! criando linhas
L,1,4
L,4,7
L,7,10
L,2,5
L,5,8
L,8,11
L,3,6
L,6,9
L,9,12
L,1,2
L,2,3
L,4,5
L,5,6
L,7,8
L,8,9
L,10,11
L,11,12
!
! criando áreas
A,1,2,5,4
A,2,3,6,5
A,4,5,8,7
A,5,6,9,8
A,7,8,11,10
A,8,9,12,11
!
! visualizando modelo
/PNUM,KP,1
/PNUM,AREA,1
/PNUM,LINE,1
APLOT
!
!************************************************************!
!****_ 		3.6 Divisões para a malha nas linhas 	_****!
!************************************************************!
! Ao longo da escavação divido de p (passo da escavação)
LSEL,ALL
LSEL,S,LOC,X,0,l3
LSEL,R,LOC,Y,l1+0.01,lt-0.01
LESIZE,ALL, , ,nL2, , , , ,1
!
! Ao longo da não-escavação (elementos com taxa de crescimento)
LSEL,ALL
LSEL,S,LOC,X,0,l3
LSEL,R,LOC,Y,0.01,l1-0.01
LESIZE,ALL, , ,nL1,mL1, , , ,1
!
! Espessura do revestimento
LSEL,ALL
LSEL,S,LOC,X,ri+0.01,re-0.01
LSEL,R,LOC,Y,0,lt
LESIZE,ALL, , ,nesp, , , , ,1
!
! Transversal solo escavado
LSEL,ALL
LSEL,S,LOC,X,0,ri-0.01
LSEL,R,LOC,Y,0,lt
LESIZE,ALL, , ,nRi, , , , ,1
!
! Ao redor da escavação
LSEL,ALL
LSEL,S,LOC,X,re+0.01,l3-0.01
LSEL,R,LOC,Y,0,lt
LESIZE,ALL, , ,nLx,mLx, , , ,1
!
!************************************************************!
!****_ 		3.9 Criando malhas 			_****!
!************************************************************!
MSHKEY,tipomalha
MSHAPE,formaelem
AMESH,1
AMESH,2
AMESH,3
AMESH,4
AMESH,5
AMESH,6
EPLOT
!
!************************************************************!
!****_ 		3.10 Grupos elementos do revestimento 	_****!
!************************************************************!
!
! gerando grupo de elementos com o revestimento do tunel
ALLSEL,ALL
ESEL,S,CENT,Y,l1,lt
ESEL,R,CENT,X,ri,re
CM,Casca1,ELEM
!
!
!************************************************************!
!****_		3.11 Alterando a cor dos elementos	_****!
!************************************************************!
ALLSEL,ALL
ASEL,S,AREA, ,1 
ASEL,A,AREA, ,3
ASEL,A,AREA, ,5
ASEL,A,AREA, ,6
ESLA,S
/COLOR,ELEM,14,ALL
!
ALLSEL,ALL
ASEL,S,AREA, ,2 
ASEL,A,AREA, ,4
ESLA,S
/COLOR,ELEM,13,ALL
!
!
ALLSEL,all
CMSEL,S,Casca1,ELEM
/COLOR,ELEM,6,ALL
!
!************************************************************!
!****_ 		3.12 Condições de contorno		_****!
!************************************************************!
! aplica tensões iniciais
ALLSEL,ALL
INISTATE, DEFINE,,,,,-pr,-pr,-pr,0,0,0
!
! simetria em torno do eixo Y
LSEL,ALL
LSEL,S,LOC,X,0,0.01
LSEL,R,LOC,Y,0,lt
DL,ALL,,SYMM
!
! simetria no topo do modelo
LSEL,ALL
LSEL,S,LOC,X,0,l3
LSEL,R,LOC,Y,lt
DL,ALL,,SYMM
!
! pressão na parte de baixo do modelo
LSEL,ALL
LSEL,S,LOC,X,0,l3
LSEL,R,LOC,Y,0
SFL,ALL,PRES,pr
!
! pressão a direita do modelo
LSEL,ALL
LSEL,S,LOC,X,l3
LSEL,R,LOC,Y,0,lt
SFL,ALL,PRES,pr
!
!************************************************************!
!****_ 		3.13 Criando elementos do revestimento	_****!
!************************************************************!
! Obs: esses elementos ficam sobrepostos ao do solo. Durante
!      a análise, conforme vai desligando os elementos do
!      macicço vai-se ligando os do resvestimento, se houver.
!
*IF,matrev,NE,0,THEN
	CMSEL,S,Casca1,ELEM
	EGEN,2,0,all,all,1,1,,,,0,0,0
	ESEL,ALL
	NUMMRG,node, , , ,LOW
	ESEL,MAT,2
	CM,Revestimento1,ELEM
*ENDIF
!
!************************************************************!
!****_ 		3.14 Grupos Esc-rev tunel  		_****!
!************************************************************!
!
! Primeira escavação
i = 1
LOCAL,11,0,0,lt,0,180,0,180,0
ESEL,ALL
fi=0
ff=npi*Lp
ESEL,S,CENT,Y,fi,ff
ESEL,R,CENT,X,0,re
CM,Lesc %i%,ELEM
CMSEL,S,Lesc %i%,ELEM
!
! Proximas escavações e resvestimentos
*DO,i,2,nesc,1
	! criando grupos com os elementos escavados do passo i
	fi=ff
	ff=(i-1)*Lp+npi*lp
	ESEL,S,CENT,Y,fi,ff
	ESEL,R,CENT,X,0,re
	CM,Lesc %i%,ELEM
	CMSEL,S,Lesc %i%,ELEM
	!
	*IF,matrev,NE,0,THEN
		! criando grupos com os elementos revestidos do passo i
		*IF,i,EQ,2,THEN
			fi=0
		*ELSE
			fi=npi*lp+(i-2)*lp-(lp+d0)
		*ENDIF
		ff = (i-1)*lp+npi*lp-(lp+d0)
		ESEL,S,CENT,Y,fi,ff
		ESEL,R,CENT,X,ri,re
		ESEL,R,MAT,,2
		MPCHG,i,ALL
		CM,Lrev %i-1%,ELEM
		CMSEL,S,Lrev %i-1%,ELEM
	*ENDIF
	!
	! revestindo o ultimo trecho
	*IF,i,EQ,nesc,THEN
		*IF,matrev,NE,0,THEN
			*IF,revested0,EQ,1,THEN
				fi=l2-lp-d0
				ff=l2
				ESEL,S,CENT,Y,fi,ff
				ESEL,R,CENT,X,ri,re
				ESEL,R,MAT,,2
				MPCHG,i,ALL
				CM,Lrev %i%,ELEM
				CMSEL,S,Lrev %i%,ELEM
			*ENDIF
			*IF,revesteface,EQ,1,THEN
				fi=l2
				ff=l2+(lp)
				ESEL,ALL
				ESEL,S,CENT,Y,fi,ff
				ESEL,R,CENT,X,0,re
				CM,soloface,ELEM
				CMSEL,S,soloface,ELEM
				!
				fi=l2
				ff=l2+lp
				ESEL,ALL
				ESEL,S,CENT,Y,fi,ff
				ESEL,R,CENT,X,0,re
				EGEN,2,0,all,all,1,1,,,,0,0,0
				NUMMRG,node, , , ,LOW
				ESEL,R,MAT,,2
				MPCHG,i,ALL
				CM,Lrevface,ELEM
				CMSEL,S,Lrevface,ELEM
			*ENDIF
		*ENDIF
	*ENDIF
*ENDDO
!
! criando grupo de elementos escavados
ESEL,S,CENT,Y,0,l2
ESEL,R,CENT,X,0,re
CM,esctotal,ELEM
CSYS,0
!
!************************************************************!
!****_ 4. SOLUÇÃO 					_****!
!************************************************************!
/SOL			
!
! Configurações iniciais
*IF,nlg,eq,0,then		
	NLGEOM,OFF
*ELSEIF,nlg,eq,1,then		
	NLGEOM,ON
*ENDIF
*IF,nr,eq,1,then		
	NROPT,INIT
*ELSEIF,nr,eq,2,then		
	NROPT,FULL
*ELSEIF,nr,eq,3,then		
	NROPT,UNSYM
*ENDIF	
*IF,psc,eq,0,then		
	PSCONTROL,ALL,OFF
*ELSEIF,nlg,eq,1,then		
	PSCONTROL,ALL,ON
*ENDIF
!
! Critério do incremento de deformação equivalente plástica
CUTCONTROL,PLSLIMIT,plslimit
!
! Gravação da saida de resultados
ALLSEL,ALL
OUTRES,ESOL,LAST
OUTRES,SVAR,ALL		
!
! Salvar parâmetros
PARSAV,ALL,parametros,txt
!
! Definindo temperatura
TUNIF,temperatura
!
! Apaga os elementos do revestimento
ALLSEL,ALL
*IF,matrev,NE,0,THEN
	CMSEL,S,Revestimento1,ELEM
	EKILL,ALL
	ESEL,ALL
	*IF,revesteface,EQ,1,THEN
		CMSEL,S,Lrevface,ELEM
		EKILL,ALL
	*ENDIF
*ENDIF
!
! Verifica o equilibrio do maciço
*IF,soleq,EQ,1,THEN
	ANTYPE,0,NEW
	ESEL,ALL
	TIME,1
	DELTIM,1,,1
	!CNVTOL,F,,tolf,,0.01
	!CNVTOL,U,,tolu
	SOLVE
	FINISH
	!
	! Plota a solução
	/POST1
	RSYS,1
	ESEL,S,LIVE
	PLNSOL,U,SUM, 0,1.0
	ESEL,ALL
*ENDIF
!
! numero máximo de análises
nanalises = nesc
*IF,analise2,EQ,1,THEN
	nanalises=nanalises + np2max
*ENDIF
!
! criando a tabela com o criterio de parada do longo prazo (np2,deltau,uatual)
*IF,analise2,EQ,1,THEN
	*DIM,criterio,TABLE,nanalises,3
*ENDIF
!
! Selecionando o nó de controle da convergencia no tempo
uatual = 0
NSEL,ALL
NSEL,S,LOC,X,0.999*Re,1.001*Re
NSEL,R,LOC,Y,lt,lt
*GET,nocontrole,NODE,0,NUM,MAX
ALLSEL,ALL
!
! Solução com as escavações
/SOL
*IF,matmacico,LE,1,AND,matrev,EQ,0,THEN
	!
	! Maciço elástico sem revestimento (não precisa rodar a cada escavação)
	ANTYPE,0,NEW
	!
	! primeira escavação
	i=1
	CMSEL,S,Lesc %i%,ELEM
	EKILL,ALL
	ESEL,S,LIVE
	EPLOT
	*DO,i,2,nesc,1
		! desligando elementos do grupo de escavação i		
		CMSEL,S,Lesc %i%,ELEM
		EKILL,ALL
		ESEL,S,LIVE
		EPLOT
		!
		! ligando elementos do grupo de revestimento i-1
		*IF,matrev,NE,0,THEN
			CMSEL,S,rev %i-1%,ELEM
			EALIVE,ALL
			*IF,revested0,EQ,1,THEN
				*IF,i,EQ,nesc,THEN
					CMSEL,S,Lrev %i%,ELEM
					EALIVE,ALL
				*ENDIF
			*ENDIF
			*IF,revesteface,EQ,1,THEN
				*IF,i,EQ,nesc,THEN
					CMSEL,S,Lrevface,ELEM
					EALIVE,ALL
					CMSEL,S,soloface,ELEM
					EKILL,ALL
				*ENDIF
			*ENDIF
		*ENDIF
		!
		! plotando apenas os elementos ativos
		ESEL,S,LIVE
		EPLOT
	*ENDDO
	ESEL,ALL
	SAVE
	!CNVTOL,F,,tolf,,0.01
	!CNVTOL,U,,tolu
	SOLVE
	FINISH
*ELSE
	!
	! Necessário fazer vários passos de escavação
	ANTYPE,0,NEW
	!
	! Primeiro passo de escavação
	i=1
	CMSEL,S,Lesc %i%,ELEM
	EKILL,ALL
	ESEL,S,LIVE
	EPLOT
	TIME,tp*npi
	DELTIM,dtp,,dtpmax
	ESEL,ALL
	!CNVTOL,F,,tolf,,0.01
	!CNVTOL,U,,tolu
	SOLVE
	SAVE
	FINISH
	!
	! Mostrar solução da primeira escavação
	/POST1
	RSYS,1
	ESEL,S,LIVE
	PLNSOL, U,SUM, 0,1.0
	!
	! Próximos passos de escavação
	/SOL
	ANTYPE,0,RESTART
	*DO,i,2,nesc,1
		!
		! desligando elementos do grupo de escavacao i
		ESEL,ALL
		CMSEL,S,Lesc %i%,ELEM
		EKILL,ALL
		ESEL,S,LIVE
		EPLOT
		!
		! ligando elementos do grupo de revestimento i-1
		*IF,matrev,NE,0,THEN
			CMSEL,S,Lrev %i-1%,ELEM
			EALIVE,ALL
			*IF,revested0,EQ,1,THEN
				*IF,i,EQ,nesc,THEN
					CMSEL,S,Lrev %i%,ELEM
					EALIVE,ALL
				*ENDIF
			*ENDIF
			*IF,revesteface,EQ,1,THEN
				*IF,i,EQ,nesc,THEN
					CMSEL,S,Lrevface,ELEM
					EALIVE,ALL
					CMSEL,S,soloface,ELEM
					EKILL,ALL
				*ENDIF
			*ENDIF
		*ENDIF
		ESEL,S,LIVE
		EPLOT
		TIME,npi*tp+tp*(i-1)
		DELTIM,dtp,,dtpmax
		ESEL,ALL
		SAVE
		!CNVTOL,F,,tolf,,0.01
		!CNVTOL,U,,tolu
		SOLVE
		!
		! Criterio de convergencia no tempo
		!criterio(i,1) = tp*(i-1)
		uanterior = uatual
		*GET,uatual,NODE,nocontrole,U,SUM
		!deltau = uatual - uanterior
		!criterio(i,2) = deltau
	*ENDDO
	!
	np2 = 0
	*IF,analise2,EQ,1,THEN
		! Faz o cálculo após o término da construção do túnel
		*DO,i,1,np2max
			AUTOTS,tp2autots
			TIME,npi*tp+tp*(nesc-1)+i*tp2
			DELTIM,dtp2,dtp2min,dtp2max,tp2carry
			SAVE
			!CNVTOL,F,,tolf2,,0.01
			!CNVTOL,U,,tolu2
			SOLVE
			!
			! Criterio de parada no longo prazo
			np2 = np2+1
			criterio(np2,1) = npi*tp+tp*(nesc-1)+i*tp2
			uanterior = uatual
			*GET,uatual,NODE,nocontrole,U,SUM
			deltau = abs(uatual - uanterior)
			criterio(np2,2) = deltau
			criterio(np2,3) = uatual
			!*IF,deltau,LT,deltaumin,*EXIT
		*ENDDO
	*ENDIF
	FINISH
*ENDIF
!
!************************************************************!
!****_ 5. PÓS-PROCESSAMENTO 				_****!
!************************************************************!
!
!************************************************************!
!****_ 		5.1 Configurações iniciais		_****!
!************************************************************!
!
! mudando a cor da janela
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15
!
! qualidade do arquivo jpeg de saida
JPEG,QUAL,100,
JPEG,ORIENT,HORIZ
JPEG,COLOR,2
JPEG,TMOD,1
/GFILE,800,
/TYPE,,3
!
!************************************************************!
!****_ 		5.2 Plotando nó de controle no tempo 	_****!
!************************************************************!
!
! Escrevendo criterio de convergencia no tempo
! criterio(:,1) - tempo
! criterio(:,2) - deltau
! criterio(:,3) - uatual
!
*IF,analise2,EQ,1,THEN
!
*CREATE,ansuitmp
*MWRITE,criterio,criterio,txt
(1000(E10.4,3X))
*END
/INPUT,ansuitmp
!
! formatando grafico para plotar uatual x tempo
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15
/AXLAB,X,t
/AXLAB,Y,u(r=Re,z=Lt)
/XRANGE,0,npi*tp+tp*(nesc-1)+np2*tp2
*VSCFUN,criteriomax,max,criterio(1,3)
/YRANGE,0,criteriomax*1.2
/GROPT,DIVY,20
/GROPT,DIVX,10
/GROPT,DIG1,3
/GTHK,CURVE,1
/PLOPTS,INFO,on
!
! graficando na tela do Ansys
/ERASE
/SHOW,JPEG,,iso
*VLEN,np2       
*VPLOT,criterio(1,1),criterio(1,3)
/SHOW,CLOSE	   
/ERASE
!
*ENDIF
!
!
!************************************************************!
!****_ 		5.3 Perfis de convergências e pressões 	_****!
!************************************************************!
/POST1	  
!
! selecionando os nós na interface entre revestimento e o maciço
NSEL,ALL
NSEL,S,LOC,X,0.999*Re,1.001*Re
!
! sistema de referencia dos resultados
RSYS,5
!
! dimensionando arrays
nanalises = 0
*GET,nanalises,ACTIVE,0,SET,NSET	! pegando o número de análises	
*GET,ncount,NODE,,COUNT			  ! pegando numero máximo de nós selecionados
*GET,ntotal,NODE,,NUM,MAX		  ! pegando numero total de nós
*DIM,convergence,TABLE,ncount,2+nanalises ! dimensionando tabela de convergencias
*DIM,pressure,TABLE,ncount,2+nanalises	  ! dimensionando tabela de pressões em x
*DIM,coordy,ARRAY,ntotal		  ! dimensionando vetor de coordenadas nodais
*DIM,u_x,ARRAY,ntotal			  ! dimensionando vetor de deslocamentos em x
*DIM,s_x,ARRAY,ntotal			  ! dimensionando vetor de pressões em x
*DIM,n_sel,ARRAY,ntotal			  ! dimensionando vetor que indica se o nó está selecionado
!
! coloca lista de nós em ordem na primeira coluna
*VGET,convergence(1,1),NODE,,NLIST
*VGET,pressure(1,1),NODE,,NLIST
!
! criando vetor de coordenadas
*VGET,n_sel(1),NODE,1,NSEL
*VGET,coordy(1),NODE,1,LOC,y
*VOPER,coordy(1),coordy(1),MULT,1/Re
!
! Preenchendo o convergence e pressure com as coordenadas
*VMASK,n_sel(1)				! usa como mascara os nós selecionados
*VFUN,convergence(1,2),COMP,coordy(1)
*VMASK,n_sel(1)
*VFUN,pressure(1,2),COMP,coordy(1)
!
! preenchendo o convergence com a convergência
*DO,i,1,nanalises,1
	! identifica o passo
	*IF,i,EQ,nanalises,THEN
		SUBSET,LAST
	*ELSE
		*IF,matmacico,LE,1,AND,matrev,EQ,0,THEN
			SUBSET,LAST
		*ELSE
			SUBSET,i,,,,,,
		*ENDIF
	*ENDIF
	!
	! Preenchendo o array com os deslocamentos radiais e tensões radiais
	*VMASK,n_sel(1)
	*VGET,u_x(1),NODE,1,U,X
	*VOPER,u_x(1),u_x(1),MULT,-1/Re*100
	!
	*VMASK,n_sel(1)
	*VGET,s_x(1),NODE,1,S,X
	*VOPER,s_x(1),s_x(1),MULT,1
	!
	! Preenchendo o convergence e pressure
	*VMASK,n_sel(1)
	*VFUN,convergence(1,2+i),COMP,u_x(1)
	*VMASK,n_sel(1)
	*VFUN,pressure(1,2+i),COMP,s_x(1)
*ENDDO
!
! ordena a tabela de acordo com a segunda coluna (das coordenadas)
*MOPER,ORDER,convergence,SORT,convergence(1,2)
*MOPER,ORDER,pressure,SORT,pressure(1,2)
!
! Coleta o maior valor das convergências
*VSCFUN,umaximofinal,max,convergence(1,2+nanalises)
*VSCFUN,smaximofinal,max,pressure(1,2+nanalises)
*VSCFUN,sminimofinal,min,pressure(1,2+nanalises)
!
! formatando grafico de convergencia
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15
/AXLAB,X,Y/Re
/AXLAB,Y,U=-u(r=Re)/Re (%)
/XRANGE,0,Lt/Re
/YRANGE,umaximofinal*1.2,0
/GROPT,DIVY,20
/GROPT,DIVX,10
/GROPT,DIG1,3
/GTHK,CURVE,1
/PLOPTS,INFO,on
!
! graficando na tela do Ansys
/ERASE
/SHOW,JPEG,,iso	       
*DO,i,1,nanalises,1
	*IF,i,EQ,nanalises,THEN
		/COLOR,CURVE,MAGE,1
	*ELSEIF,i,EQ,nesc,THEN
		/COLOR,CURVE,YELL,1
	*ELSE
		/COLOR,CURVE,LGRA,1
	*ENDIF
	*VPLOT,convergence(1,2),convergence(1,2+i)
	/NOERASE
*ENDDO
/SHOW,CLOSE	   
/ERASE
!
!
! formatando grafico do pressure
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15
/AXLAB,X,Y/Re
/AXLAB,Y,Sx (MPa)
/XRANGE,0,Lt/Re
/YRANGE,smaximofinal*1.2,sminimofinal*1.2
/GROPT,DIVY,20
/GROPT,DIVX,10
/GROPT,DIG1,3
/GTHK,CURVE,1
/PLOPTS,INFO,on
!
! graficando na tela do Ansys
/ERASE
/SHOW,JPEG,,iso	       
*DO,i,1,nanalises,1
	*IF,i,EQ,nanalises,THEN
		/COLOR,CURVE,MAGE,1
	*ELSEIF,i,EQ,nesc,THEN
		/COLOR,CURVE,YELL,1
	*ELSE
		/COLOR,CURVE,LGRA,1
	*ENDIF
	*VPLOT,pressure(1,2),pressure(1,2+i)
	/NOERASE
*ENDDO
/SHOW,CLOSE	   
/ERASE
!
! Escrevendo em arquivo as convergências
*CREATE,ansuitmp
*MWRITE,convergence,convergencias,txt
(1000(E10.4,3X))
*END
/INPUT,ansuitmp
!
! Escrevendo em arquivo as pressões
*CREATE,ansuitmp
*MWRITE,pressure,pressure,txt
(1000(E10.4,3X))
*END
/INPUT,ansuitmp
!
! Cria arquivo com os parâmetros
PARSAV,SCALAR,parametros,txt
!
!
!****_ 			FIM				_****!
